apply plugin: 'com.android.application'
apply plugin: "com.github.spotbugs"

// spot bug implementation referenced from https://stackoverflow.com/questions/52643805/minimal-working-spotbugs-setup-for-android-studio

android {

    compileSdkVersion rootProject.ext.rtCompileSdkVersion
    buildToolsVersion rootProject.ext.rtBuildToolsVersion
    defaultConfig {
        applicationId "cmu.xprize.robotutor"
        minSdkVersion rootProject.ext.rtMinSdkVersion
        targetSdkVersion rootProject.ext.rtTargetSdkVersion
        versionCode rootProject.ext.rtVersionCode
        versionName rootProject.ext.rtVersionName
        // default is signed by the Android default key

        multiDexEnabled true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    // Create a variable called keystorePropertiesFile, and initialize it to your
    // keystore.properties file, in the rootProject folder.
    def keystorePropertiesFile = rootProject.file("keystore.properties")

    // Initialize a new Properties() object called keystoreProperties
    def keystoreProperties = new Properties()

    // Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    signingConfigs {
        android {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.android
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }

        // This release includes the DEBUG selector and other various debug support features.
        //
        release_dbg {
            debuggable true
            signingConfig signingConfigs.android
        }

    }

    // define apk naming behavior
    // define apk naming behavior
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def project = "robotutor"
            def SEP = "."
            def buildType = variant.buildType.name
            def version = variant.versionName

            def newApkName = project + SEP + buildType + SEP + version + ".apk"
//            new File(output.outputFile.parent, newApkName)
            output.outputFileName = newApkName
        }
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet false
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings false
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:25.2.0'
    implementation 'com.android.support:percent:25.2.0'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation project(':comp_banner')
    implementation project(':comp_ltkplus')
    implementation project(':util')
    implementation project(':mn_component')
    implementation project(':comp_listener')
    implementation project(':comp_reading')
    implementation project(':comp_questions')
    implementation project(':sm_component')
    implementation project(':fw_component')
    implementation project(':nl_component')
    implementation project(':comp_math')
    implementation project(':comp_akira')
    implementation project(':comp_bubblepop')
    implementation project(':comp_writing')
    implementation project(':comp_pointtap')
    implementation project(path: ':comp_ask')
    implementation project(path: ':comp_session')
    implementation project(path: ':comp_debug')
    implementation project(':comp_logging')
    implementation project(path: ':comp_counting')
    implementation project(path: ':comp_counting2')
    implementation project(path: ':comp_numberscale')
    implementation project(path: ':comp_nd')
    implementation project(path: ':comp_picmatch')
    implementation project(path: ':comp_bigmath')
    implementation project(path: ':comp_spelling')

    implementation project(path: ':comp_intervention')
}

// For SpotBugs to create 'spotbugsMain' gradle task
sourceSets {
    main {
        java.srcDirs = []
    }
}

spotbugs {
    ignoreFailures = true
    reportsDir = file("$project.buildDir/SpotBugsReports")
    effort = "max"
    reportLevel = "high"
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    dependsOn 'assembleDebug'
    classes = files("$project.buildDir/intermediates/javac") // Important to use this path
    excludeFilter = file("$project/spot-bugs-exclude.xml") // Optional - Explained below
    source = fileTree('src/main/java')
    reports {
        // Enable HTML report only
        html.enabled = true
        xml.enabled = false
    }
}


